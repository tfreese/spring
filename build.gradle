import org.springframework.boot.gradle.plugin.SpringBootPlugin

// Execute Tasks in SubModule: gradle MODUL:clean build
plugins {
    id("de.freese.gradle.conventions").version("$version_myJavaConventionPlugin").apply(false)
    // id("io.spring.dependency-management").version("$version_springDependencyManagementPlugin").apply(false)
    id("org.openjfx.javafxplugin").version("$version_javafxPlugin").apply(false)
    id("org.springframework.boot").version("$version_springBoot").apply(false)
}

allprojects {
    apply(plugin: "base")

    ant.lifecycleLogLevel = "INFO"

    // repositories {
    //
    // }
}

subprojects {
    apply(plugin: "de.freese.gradle.conventions")
    apply(plugin: "io.spring.dependency-management")

    dependencyManagement {
        imports {
            mavenBom("de.codecentric:spring-boot-admin-dependencies:$version_springBootAdmin")
            mavenBom("org.springframework.ai:spring-ai-bom:$version_springAi")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:$version_springCloud")
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
            // mavenBom("org.springframework.boot:spring-boot-dependencies:$version_springBoot")
        }

        dependencies {
            dependency("com.atomikos:transactions-spring-boot3-starter:$version_atomicosSpringBootStarter")
            dependency("com.esotericsoftware:kryo:$version_kryo")

            dependencySet(group: "com.github.tjake", version: "$version_jlama") {
                entry("jlama-core")
                entry("jlama-native")
            }

//            dependency("de.javakaffee:kryo-serializers:$version_kryoSerializers")

            dependency("jakarta.faces:jakarta.faces-api:$version_jakartaFacesApi")
            dependency("jakarta.platform:jakarta.jakartaee-api:$version_jakartaApi")
            dependency("jakarta.platform:jakarta.jakartaee-web-api:$version_jakartaApi")

            dependencySet(group: "org.apache.qpid", version: "$version_qpid") {
                entry("qpid-broker") {
                    exclude(group: "ch.qos.logback", name: "logback-core")
                    exclude(group: "ch.qos.logback", name: "logback-classic")
                }
                entry("qpid-bdbstore") {
                    exclude(group: "com.sleepycat", name: "je")
                }
            }

            // dependencySet(group: "org.apache.tomcat", version: dependencyManagement.importedProperties["tomcat.version"]) {
            //     entry("tomcat-embed-jasper")
            // }

            dependency("org.glassfish:jakarta.faces:$version_glassfishJakartaFaces")

            dependency("org.jboss.weld.servlet:weld-servlet-core:$version_weld")
            dependency("org.joinfaces:primefaces-spring-boot-starter:$version_joinfaces")

            dependency("org.neo4j:neo4j:$version_neo4j")


            dependencySet(group: "org.openjdk.jmh", version: "$version_jmh") {
                entry("jmh-core")
                entry("jmh-generator-annprocess")
            }

            dependency("org.primefaces:primefaces:$version_primefaces")
            dependency("org.primefaces.extensions:primefaces-extensions:$version_primefacesExtensions")
            dependency("org.primefaces:primefaces-themes:$version_primefacesThemes")
            dependency("org.springdoc:springdoc-openapi-starter-webmvc-ui:$version_springDoc")

            dependencySet(group: "org.springframework.cloud", version: "$version_springCloudNetflix") {
                entry("spring-cloud-starter-netflix-ribbon")
                entry("spring-cloud-starter-netflix-hystrix")
                entry("spring-cloud-starter-netflix-hystrix-dashboard")
            }

            dependency("software.xdev:chartjs-java-model:$version_chartjsJavaModel")
        }
    }

    pluginManager.withPlugin("java") {
        configurations {
            mockitoAgent
        }

        dependencies {
            // implementation(platform("de.freese:maven-bom:$version_mavenBom"))

            runtimeOnly("org.springframework.boot:spring-boot-properties-migrator")

            testImplementation("org.awaitility:awaitility")
            testImplementation("org.junit.jupiter:junit-jupiter")
            testImplementation("org.springframework.boot:spring-boot-starter-test")
            testRuntimeOnly("org.junit.platform:junit-platform-launcher")

            testImplementation("org.mockito:mockito-junit-jupiter")
            mockitoAgent("org.mockito:mockito-core") {
                transitive = false
            }
        }

        // test {
        // tasks.named("test")
        tasks.withType(Test).configureEach {
            enabled = true
            ignoreFailures = false
            useJUnitPlatform()

            String mockitoFiles = configurations.mockitoAgent.asPath

            doFirst {
                jvmArgs += "-javaagent:" + mockitoFiles
                // jvmArgs += "-XX:+EnableDynamicAgentLoading"
            }
        }
    }
}

// For Composite Build.
tasks.register("cleanAll", GradleBuild) {
    tasks = ["clean"]
}
tasks.register("buildAll", GradleBuild) {
    tasks = ["build"]
}

// project("kubernetes") {
//    apply plugin: "org.springframework.boot"
//    description = "Folder project"
//    // All SubProjects have Plugin 'org.springframework.boot' that requires a mainClass.
//    springBoot {
//        mainClass = "disabled"
//    }
//}
//
// project("spring-cloud") {
//    apply plugin: "org.springframework.boot"
//    description = "Folder project"
//    // All SubProjects have Plugin 'org.springframework.boot' that requires a mainClass.
//    springBoot {
//        mainClass = "disabled"
//    }
//}
//
// project("spring-messaging") {
//    apply plugin: "org.springframework.boot"
//    description = "Folder project"
//    // All SubProjects have Plugin 'org.springframework.boot' that requires a mainClass.
//    springBoot {
//        mainClass = "disabled"
//    }
//}
